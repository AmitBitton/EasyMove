plugins {
    alias(libs.plugins.android.application)
    id 'com.google.gms.google-services'
}

// טעינת מפתח ה-API מקובץ local.properties
def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withInputStream { localProperties.load(it) }
}
def googleMapsKey = localProperties.getProperty('GOOGLE_MAPS_API_KEY', "")

android {
    namespace 'com.example.easymove'
    compileSdk 34

    defaultConfig {
        applicationId "com.example.easymove"
        minSdk 24
        targetSdk 34
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // הזרקת המפתח לתוך ה-Manifest
        manifestPlaceholders = [googleMapsApiKey: googleMapsKey]

        // יצירת משתנה שאפשר להשתמש בו גם בקוד Java (למשל: BuildConfig.MAPS_KEY)
        buildConfigField "String", "MAPS_KEY", "\"${googleMapsKey}\""

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    // חובה כדי להשתמש ב-BuildConfig שנוצר למעלה
    buildFeatures {
        buildConfig true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.activity:activity:1.9.0'
    implementation 'com.firebase:geofire-android-common:3.2.0'
    implementation 'com.google.android.gms:play-services-auth:20.7.0'

    implementation platform('com.google.firebase:firebase-bom:33.1.0')
    implementation 'com.google.firebase:firebase-auth'
    implementation 'com.google.firebase:firebase-firestore'
    implementation 'com.google.firebase:firebase-storage'
    implementation 'com.google.firebase:firebase-analytics'

    implementation 'com.google.android.gms:play-services-maps:19.0.0'
    implementation 'com.google.android.libraries.places:places:3.5.0'

    implementation 'com.github.bumptech.glide:glide:4.16.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.16.0'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}